/*
 * Copyright (c) 2017 escoand.
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-crash'
apply from: 'versioning.gradle'

version = "1.1.0"

android {
    compileSdkVersion 25
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "de.escoand.readdaily"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode buildVersionCode()
        versionName version

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        // archive apk and mapping
        applicationVariants.all { variant ->
            if (variant.getBuildType().isMinifyEnabled()) {
                variant.assemble.doLast {
                    copy {
                        def archiveName = "${flavorName}-${variant.buildType.name}-${variant.versionCode}"
                        from variant.outputs[0].outputFile, variant.mappingFile
                        into "${rootDir}/archive"
                        rename "app-${flavorName}-${variant.buildType.name}.apk", "app-${archiveName}.apk"
                        rename "mapping.txt", "map-${archiveName}.txt"
                    }
                }
            }
        }
    }

    flavorDimensions "default"

    productFlavors {
        tmg {
            applicationId "de.brunnen_verlag.termine_mit_gott"
            dimension "default"
        }
        ls {
            applicationId "de.bornverlag.lichtstrahlen"
            dimension "default"
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    // google
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:support-annotations:25.3.1'
    compile 'com.google.android.gms:play-services-gcm:11.2.2'
    compile 'com.google.firebase:firebase-core:11.2.2'
    compile 'com.google.firebase:firebase-crash:11.2.2'
    compile 'com.google.firebase:firebase-messaging:11.2.2'
    // 3rd party
    compile 'com.anjlab.android.iab.v3:library:1.0.44'
    compile 'com.prolificinteractive:material-calendarview:1.4.3'
    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    // tmg flavor
    tmgCompile 'de.hdodenhof:circleimageview:2.1.0'
    tmgCompile 'uk.co.chrisjenx:calligraphy:2.2.0'
    // testing
    androidTestCompile 'com.android.support:support-annotations:25.3.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
}

apply plugin: 'com.google.gms.google-services'
