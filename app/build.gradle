/*
 * Copyright (c) 2017 escoand.
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-crash'
apply from: 'versioning.gradle'

version = "1.1.0-beta3"

android {
    compileSdkVersion 25
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "de.escoand.readdaily"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode buildVersionCode()
        versionName version

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        // archive apk and mapping
        applicationVariants.all { variant ->
            if (variant.getBuildType().isMinifyEnabled()) {
                variant.assemble.doLast {
                    copy {
                        def archiveName = "${flavorName}-${variant.buildType.name}-${variant.versionCode}"
                        from variant.outputs[0].outputFile, variant.mappingFile
                        into "${rootDir}/archive"
                        rename "app-${flavorName}-${variant.buildType.name}.apk", "app-${archiveName}.apk"
                        rename "mapping.txt", "map-${archiveName}.txt"
                    }
                }
            }
        }
    }
    productFlavors {
        tmg {
            applicationId "de.brunnen_verlag.termine_mit_gott"
        }
        ls {
            applicationId "de.bornverlag.lichtstrahlen"
        }
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // google
    compile 'com.android.support:cardview-v7:25.1.0'
    compile 'com.android.support:design:25.1.0'
    compile 'com.google.android.gms:play-services-gcm:10.0.1'
    compile 'com.google.firebase:firebase-core:10.0.1'
    compile 'com.google.firebase:firebase-crash:10.0.1'
    compile 'com.google.firebase:firebase-messaging:10.0.1'

    // 3rd party
    compile 'com.anjlab.android.iab.v3:library:1.0.38'
    compile 'com.prolificinteractive:material-calendarview:1.4.2'
    compile 'com.squareup.okhttp3:okhttp:3.5.0'

    // tmg flavor
    tmgCompile 'de.hdodenhof:circleimageview:2.1.0'
    tmgCompile 'uk.co.chrisjenx:calligraphy:2.2.0'

    // testing
    androidTestCompile 'com.android.support:support-annotations:25.1.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
}

apply plugin: 'com.google.gms.google-services'