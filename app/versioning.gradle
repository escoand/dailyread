/*
 * Copyright (c) 2016 escoand.
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

ext {
    getVersionCode = {
        def candidate = 99
        def (major, minor, patch) = version.toLowerCase().tokenize(".")

        if (patch.contains("-devel")) {
            patch = patch.replaceAll("[^0-9]", "")
            candidate = 0
        } else if (patch.contains("-alpha")) {
            (patch, candidate) = patch.tokenize("-")
            candidate = 0 + candidate.replaceAll("[^0-9]", "").toInteger()
        } else if (patch.contains("-beta")) {
            (patch, candidate) = patch.tokenize("-")
            candidate = 50 + candidate.replaceAll("[^0-9]", "").toInteger()
        }

        (major, minor, patch, candidate) = [major, minor, patch, candidate].collect {
            it.toInteger()
        }
        (major * 1000000) + (minor * 10000) + (patch * 100) + candidate
    }
}